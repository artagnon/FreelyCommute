ident = _{ id | "{" ~ id ~ "}" }
  id = @{ (idprim | mathbb | greek | bullet | exists | special) }
    idprim = _{ (alpha | digit)+ }
      alpha = _{ 'a'..'z' | 'A'..'Z' }
      digit = _{ '0'..'9' }
    mathbb = @{ "\\mathbb{" ~ id ~ "}" }
    greek = @{ "\\alpha" | "\\beta" | "\\gamma" | "\\delta" | "\\pi" | "\\lambda" | "\\phi" | "\\theta" }
  bullet = @{ "\\bullet" }
  exists = @{ ("\\exists" | "\\exists!") }
  special = @{ "\\circ" | "\\times" | "\\langle" | "\\rangle" }

suborsuper = _{ superscr | subscr }
  superscr = @{ "^" }
  subscr = @{ "_" }

node = _{ ident+ ~ (suborsuper ~ ident+)* }

arrow = _{ ar ~ label_mid? ~ (suborsuper ~ label_shift? ~ node)* }
  ar = _{ "\\ar" ~ ("@" ~ ar_curvature? ~ "{" ~ ar_style? ~ "}")? ~ ar_directions }
    ar_style = @{ "=>" | ".>" | ":>" | "~>" | "-->" | "-" }
    ar_directions = @{ ("[" ~ ("l"|"r"|"u"|"d")+ ~ "]")+ }
    ar_curvature = @{ "/" ~ suborsuper ~ "/" }
    label_shift = @{ ("<" | ">")+ }
    label_mid = @{ "|" ~ node }

main = _{ SOI ~ linenl* ~ lastline ~ EOI }
  linenl = _{ line ~ ("\\\\" ~ NEWLINE) }
  lastline = _{ line ~ "\\\\"? ~ NEWLINE }
    line = _{ amp* ~ (node ~ arrow? ~ amp*)* }
      amp = @{ "&" }

WHITESPACE = _{ " " | "\t" }
